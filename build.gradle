apply from: 'config.gradle'

buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('com.github.ben-manes:gradle-versions-plugin:0.17.0')
		classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
	}
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'maven'
	apply plugin: "com.moowork.node"
	group = 'com.pigatron.web'
	version = '0.0.1-SNAPSHOT'
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		compile('org.apache.commons:commons-lang3:3.4')

		testCompile("org.easytesting:fest-assert-core:2.0M10")
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.gradle:gradle-tooling-api:2.14')
	}
	sourceSets {
		integrationTest {
			java {
				compileClasspath += main.output + test.output
				runtimeClasspath += main.output + test.output
				srcDirs = ["src/test-integration/java"]
			}
			resources.srcDir file('src/test-integration/resources')
		}
		functionalTest {
			java {
				compileClasspath += main.output + test.output + integrationTest.output
				runtimeClasspath += main.output + test.output + integrationTest.output
				srcDirs = ["src/test-functional/java"]
			}
			resources.srcDir file('src/test-functional/resources')
		}
	}
	configurations {
		integrationTestCompile.extendsFrom testCompile
		integrationTestRuntime.extendsFrom testRuntime
		functionalTestCompile.extendsFrom integrationTestCompile
		functionalTestRuntime.extendsFrom integrationTestRuntime
	}
	idea {
		module {
			excludeDirs += file('node_modules')
			excludeDirs += file('bower_components')
			excludeDirs += file('typings')
			testSourceDirs += file('src/test-integration/java')
			testSourceDirs += file('src/test-functional/java')
		}
	}
	node {
		version = '8.2.1'
		download = true
	}
	task integrationTest(type: Test) {
		testClassesDir = sourceSets.integrationTest.output.classesDir
		classpath = sourceSets.integrationTest.runtimeClasspath
		outputs.upToDateWhen { false }
	}
	task functionalTest(type: Test) {
		testClassesDir = sourceSets.functionalTest.output.classesDir
		classpath = sourceSets.functionalTest.runtimeClasspath
		outputs.upToDateWhen { false }
	}
}

subprojects {
	apply plugin: "com.moowork.node"

	// Web Resource Handling
	task webpack(type: NodeTask) {
		doFirst {
			if (!file("webpack.config.js").exists() ) {
				throw new StopExecutionException()
			}
		}
		script = file('../node_modules/webpack/bin/webpack.js')
		inputs.file file("webpack.config.js")
		inputs.file file("typings.json")
		inputs.file file("tsconfig.json")
		inputs.dir file("src/main/resources/static")
		outputs.dir file("$buildDir/resources/main/static")
		//dependsOn npmInstall
	}

	processResources.dependsOn webpack

	task protractorTests(type: NodeTask) {
		doFirst {
			if (!file('src/test/resources/protractor.conf.js').exists()) {
				throw new StopExecutionException()
			}
		}
		script = file('../node_modules/protractor/bin/protractor')
		args = ['src/test/resources/protractor.conf.js']
		dependsOn ':updateWebDriver'
	}

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
}

task updateWebDriver(type: NodeTask) {
	script = file('node_modules/protractor/bin/webdriver-manager')
	args = ['update', '--standalone']
}

jar {
	baseName = 'pigatron-web'
	version = versionName
}
sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
	compile project(":web-public-shop")
	compile project(":web-admin-shop")
	compile project(":web-server-shop")
}

task dependencyReport(type: NodeTask) {
	script = file('scripts/dependencyReport.js')
	args = []
	dependsOn dependencyUpdates
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.6'
}

task buildInfo {
	def cmd = "git describe --long --tags --dirty --always"
	def proc = cmd.execute()
	ext.revision = proc.text.trim()
	def props = new Properties()
	props.setProperty("pigatron.revision", ext.revision)
	def propFile = new File("./application.properties")
	propFile.createNewFile()
	props.store(propFile.newWriter(), null)
}
bootJar.dependsOn buildInfo